buildscript {
    repositories {
        mavenCentral()
    }
}

plugins {
    id 'org.springframework.boot' version '2.4.2'
    id "com.gorylenko.gradle-git-properties" version "2.2.4"
    id "io.freefair.lombok" version "5.3.0"
}

springBoot {
    buildInfo {
        properties {
            artifact = 'formplayer'
            version = new Date().format('yyyy-MM-dd HH:mm:ss')
            group = 'org.commcare'
            name = 'Formplayer'
        }
    }
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'io.spring.dependency-management'

jar {
    archiveBaseName = 'formplayer'
}

allprojects{
    apply plugin: "java"
}

repositories {
    mavenCentral()
}

ext {
    isTravisBuild = System.getenv().get("TRAVIS") == 'true'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

idea {
    module {
        excludeDirs += [file("libs/commcare/src/jmh")]
    }
}

configurations {
    developmentOnly
    runtimeClasspath {
        extendsFrom developmentOnly
    }
}

dependencies {

    //Versions managed by Spring Boot Dependency Management
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind'
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-core'
    implementation group: 'commons-codec', name: 'commons-codec'
    implementation group: 'org.apache.commons', name: 'commons-lang3'
    implementation group: 'org.apache.httpcomponents', name: 'httpclient'
    implementation group: 'org.flywaydb', name: 'flyway-core'
    implementation group: 'org.postgresql', name: 'postgresql'
    implementation group: 'org.springframework.boot', name: 'spring-boot'
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-web'
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-actuator'
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter'
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-jdbc'
    implementation group: 'org.springframework.boot', name: 'spring-boot-autoconfigure'
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-data-jpa'
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-cache'
    implementation group: 'org.xerial', name: 'sqlite-jdbc'
    implementation group: 'redis.clients', name: 'jedis'

    //Unmanaged Dependencies
    implementation group: 'com.datadoghq', name: 'java-dogstatsd-client', version: '2.3'
    implementation group: 'io.sentry', name: 'sentry', version: '1.7.28'
    implementation group: 'org.apache.directory.studio', name: 'org.apache.commons.io', version: '2.4'
    implementation group: 'org.json', name: 'json', version: '20201115'
    implementation group: 'org.simpleframework', name: 'simple-xml', version: '2.7.1'
    implementation group: 'org.springframework', name: 'spring-web', version: '5.2.0.RELEASE'
    implementation group: 'org.springframework', name: 'spring-jdbc', version: '5.2.0.RELEASE'
    implementation group: 'org.springframework.data', name: 'spring-data-jpa', version: '2.2.0.RELEASE'
    implementation group: 'org.springframework.data', name: 'spring-data-redis', version: '2.2.0.RELEASE'
    implementation group: 'org.springframework.data', name: 'spring-data-commons', version: '2.2.0.RELEASE'
    implementation group: 'org.springframework.integration', name: 'spring-integration-redis', version: '5.2.0.RELEASE'
    implementation group: 'net.javacrumbs.shedlock', name: 'shedlock-spring', version: '4.14.0'
    implementation group: 'net.javacrumbs.shedlock', name: 'shedlock-provider-jdbc-template', version: '4.14.0'
    implementation group: 'io.micrometer', name: 'micrometer-registry-statsd', version: '1.3.0'
    runtimeOnly group: 'com.github.ben-manes.caffeine', name: 'jcache', version: '2.8.8'

    implementation fileTree(dir: 'libs', include: '*.jar')
    implementation project(path: ':commcare', configuration: 'api')
    implementation project(path: ':commcare', configuration: 'translate')

    testImplementation project(path: ':commcare', configuration: 'api')

    //Versions managed by Spring Dependency Management
    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }

    //Unmanaged Dependencies
    testImplementation group: 'com.jayway.jsonpath', name: 'json-path', version: '2.5.0'
    testImplementation group: 'com.jayway.jsonpath', name: 'json-path-assert', version: '2.5.0'
    testImplementation group: 'com.google.guava', name: 'guava', version: '30.1-jre'
    testImplementation group: 'org.mockito', name: 'mockito-junit-jupiter', version: '3.7.7'
    testImplementation group: 'org.mockito', name: 'mockito-core', version: '3.7.7'
    testImplementation group: 'org.mockito', name: 'mockito-inline', version: '3.7.7'

    testRuntimeOnly group: 'org.skyscreamer', name: 'jsonassert', version: '1.5.0'

    developmentOnly group: 'org.springframework.boot', name: 'spring-boot-devtools'

}

test {
    useJUnitPlatform()
    testLogging {
        exceptionFormat 'full'
        events "passed", "skipped", "failed"
    }
    if (isTravisBuild) {
        System.out.print('Is Travis test run')
        maxParallelForks = 1
        maxHeapSize = '768m'
        forkEvery = 100   // helps when tests leak memory
        afterSuite {
            System.out.print('.')
            System.out.flush()
        }
    }
}

task CreateSnapshotDbs(type: Test) {
    environment('org.commcare.formplayer.test.snapshot.mode', 'write')
    filter {
        //specific test method
        includeTestsMatching "org.commcare.formplayer.utils.GenerateSnapshotDatabases"
    }
}

task copyTestResources(type: Copy) {
    from "${projectDir}/src/test/resources"
    into "${buildDir}/classes/test"
}
test.dependsOn copyTestResources

task copyScripts(type: Copy) {
    from "${projectDir}/scripts/archive_dbs.sh"
    into "${buildDir}/scripts"
}

assemble.dependsOn copyScripts
